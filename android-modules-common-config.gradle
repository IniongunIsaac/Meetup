if (project.name != "app") {
    apply plugin: "com.android.library"
}
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-kapt"
apply plugin: "de.mannodermaus.android-junit5"

android {

    compileSdkVersion DepVersions.compileSdkVersion

    buildToolsVersion DepVersions.buildToolsVersion


    defaultConfig {

        minSdkVersion DepVersions.minSdkVersion
        targetSdkVersion DepVersions.targetSdkVersion
        versionCode DepVersions.versionCode
        versionName DepVersions.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'

    }

    buildTypes {

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            testCoverageEnabled true
        }
    }

    compileOptions {
        sourceCompatibility = DepVersions.java
        targetCompatibility = DepVersions.java
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    testOptions {
        animationsDisabled true
        unitTests {
            includeAndroidResources = true
        }
    }

}

dependencies {

    implementation(
            fileTree(dir: 'libs', include: ['*.jar']),
            Dependencies.kotlinStdLib,
            Dependencies.androidXCoreKts,

            Dependencies.javax,
            Dependencies.javaxAnnotation
    )

    if (project.name != "entities") {
        implementation(
                project(Modules.entities)
        )
    }

    androidTestImplementation(
            Dependencies.androidXTestCore,
            Dependencies.androidXTestRunner,
            Dependencies.androidXTestRules,
            Dependencies.androidXArchCoreTesting,
            Dependencies.androidXJunitExtension,
            //Dependencies.junit5JupiterApi,
            Dependencies.junit5JupiterParams
    )

    androidTestImplementation(Dependencies.junit5JupiterApi) {
        exclude group: "org.junit.platform", module: "junit-platform-commons"
    }

    testImplementation(
            Dependencies.junit,
            Dependencies.mockK,
            //Dependencies.junit5JupiterApi,
            Dependencies.junit5JupiterParams,
            Dependencies.hamcrest,
            Dependencies.roboelectric
    )

    testImplementation(Dependencies.junit5JupiterApi) {
        exclude group: "org.junit.platform", module: "junit-platform-commons"
    }

    testRuntimeOnly(Dependencies.junit5JupiterEngine) {
        exclude group: "org.junit.platform", module: "junit-platform-commons"
    }

    androidTestRuntimeOnly(Dependencies.junit5JupiterEngine) {
        exclude group: "org.junit.platform", module: "junit-platform-commons"
    }

}